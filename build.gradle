plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'ru.vyarus.quality' version '6.0.1'
    id 'com.github.spotbugs' version '6.4.8'
    id 'net.researchgate.release' version '3.0.2'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

java {
    sourceCompatibility = 1.8
}

wrapper {
    gradleVersion = '8.14.4'
}

repositories { mavenLocal(); mavenCentral() }
dependencies {
    implementation platform('org.junit:junit-bom:5.11.4')

    // direct dependency to prevent usage with older junit api
    implementation 'org.junit.jupiter:junit-jupiter-api'

    // to not force exact version in user classpath
    provided 'org.spockframework:spock-core:2.3-groovy-4.0'

    testImplementation 'org.junit.platform:junit-platform-testkit'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter'
}

group = 'ru.vyarus'
description = 'Junit 5 extensions support for Spock Framework 2'

github {
    user = 'xvik'
    license = 'MIT'
}

maven {
    pom {
        developers {
            developer {
                id = 'xvik'
                name = 'Vyacheslav Rusakov'
                email = 'vyarus@gmail.com'
            }
        }
    }
    // use only direct dependencies in the generated pom, removing BOM mentions
    removeDependencyManagement()
}

javaLib {
    // java 9 auto module name
    autoModuleName = 'ru.vyarus.spock.junit5'
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}



nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign).configureEach {
    onlyIf { !System.getenv('JITPACK') && !it.version.endsWith('-SNAPSHOT') }
}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
